cmake_minimum_required(VERSION 3.22.1)
find_package(Python 3.10 REQUIRED COMPONENTS Interpreter NumPy)

file(REAL_PATH "CTestTestfile.cmake" CMAKE_CURRENT_SOURCE_FILE)
cmake_path(GET CMAKE_CURRENT_SOURCE_FILE PARENT_PATH CMAKE_CURRENT_SOURCE_DIR)
cmake_path(SET CMAKE_SOURCE_DIR NORMALIZE "${CMAKE_CURRENT_SOURCE_DIR}/../..")
cmake_path(SET COVERAGE_RCFILE NORMALIZE "${CMAKE_SOURCE_DIR}/pyproject.toml")

function(swarmrl_add_test)
  cmake_parse_arguments(TEST "" "PATH;THREADS" "LABELS" ${ARGN})
  cmake_path(GET TEST_PATH STEM TEST_NAME)
  if(DEFINED ENV{COVERAGE} AND "$ENV{COVERAGE}" STREQUAL "1")
    list(APPEND PYTHON_ARGUMENTS "-m" "coverage" "run" "--source=${CMAKE_SOURCE_DIR}" "--rcfile=${COVERAGE_RCFILE}")
  endif()
  add_test(${TEST_NAME} "${Python_EXECUTABLE}" ${PYTHON_ARGUMENTS} "${TEST_PATH}")
  set_tests_properties(${TEST_NAME} PROPERTIES SKIP_RETURN_CODE 5)
  set_tests_properties(${TEST_NAME} PROPERTIES LABELS ${TEST_LABELS})
  if(DEFINED TEST_THREADS)
    set_tests_properties(${TEST_NAME} PROPERTIES PROCESSORS ${TEST_THREADS})
  endif()
endfunction()

swarmrl_add_test(PATH integration_tests/test_rl_trainers.py LABELS long)
swarmrl_add_test(PATH integration_tests/test_full_simulation.py LABELS long)
swarmrl_add_test(PATH unit_tests/test_add_colloid_on_point.py)
swarmrl_add_test(PATH unit_tests/test_add_walls.py)
swarmrl_add_test(PATH unit_tests/test_confining_walls.py)
swarmrl_add_test(PATH unit_tests/test_espresso.py)
swarmrl_add_test(PATH unit_tests/test_espresso_2d.py)
swarmrl_add_test(PATH unit_tests/test_flow.py)
swarmrl_add_test(PATH unit_tests/test_integration.py)
swarmrl_add_test(PATH unit_tests/test_lattice_boltzmann.py)
swarmrl_add_test(PATH unit_tests/test_rod.py)
